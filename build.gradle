plugins {
    id "java"
    id "maven-publish"
    id "signing"
    id "checkstyle"
    id "org.embulk.embulk-plugins" version "0.5.2"
}

repositories {
    mavenCentral()

    // Tests need EmbulkTestRuntime. EmbulkTestRuntime needs old-style embulk-standards till 0.10.28. 0.10.28 needs jcenter.
    // Tests call FileInputRunner directly. FileInputRunner needs old-style plugin architecture like 0.10.19. 0.10.19 needs jcenter.
    jcenter()
}

group = "org.embulk"
version = "0.4.0-SNAPSHOT"
description = "Reads files stored on a FTP server."

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
    options.encoding = "UTF-8"
}

java {
    withJavadocJar()
    withSourcesJar()
}

dependencies {
    compileOnly "org.embulk:embulk-api:0.10.19"
    compileOnly "org.embulk:embulk-spi:0.10.19"

    compile("org.embulk:embulk-util-config:0.3.1") {
        // They conflict with embulk-core. They are once excluded here,
        // and added explicitly with versions exactly the same with embulk-core:0.10.19.
        exclude group: "com.fasterxml.jackson.core", module: "jackson-annotations"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-core"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"
        exclude group: "com.fasterxml.jackson.datatype", module: "jackson-datatype-jdk8"
        exclude group: "javax.validation", module: "validation-api"
    }

    // They are once excluded from transitive dependencies of other dependencies,
    // and added explicitly with versions exactly the same with embulk-core:0.10.19.
    compile "com.fasterxml.jackson.core:jackson-annotations:2.6.7"
    compile "com.fasterxml.jackson.core:jackson-core:2.6.7"
    compile "com.fasterxml.jackson.core:jackson-databind:2.6.7"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.6.7"
    compile "javax.validation:validation-api:1.1.0.Final"

    compile "org.embulk:embulk-util-ssl:0.3.0"
    compile files("${project.rootDir}/libs/ftp4j-1.7.2.jar")
    compile "org.bouncycastle:bcpkix-jdk15on:1.52"

    compile "org.embulk:embulk-util-file:0.1.3"
    compile "org.embulk:embulk-util-retryhelper:0.8.2"

    testCompile "junit:junit:4.13.2"
    testCompile "org.embulk:embulk-core:0.10.19"
    testCompile "org.embulk:embulk-core:0.10.19:tests"
    testCompile "org.embulk:embulk-standards:0.10.19"
    testCompile "org.embulk:embulk-deps:0.10.19"
}

embulkPlugin {
    mainClass = "org.embulk.input.ftp.FtpFileInputPlugin"
    category = "input"
    type = "ftp"

    // "it.sauronsoftware:ftp4j:1.7.2" is not released in Maven Central, then,
    // developers cannot build embulk-input-ftp with the depencency configuration.
    //
    // But, pom.xml is used also for a Maven-based plugin installation and loading.
    // If an Embulk user specifies an appropriate Maven repository (ex. their own)
    // which has "it.sauronsoftware:ftp4j:1.7.2" when installing embulk-input-ftp,
    // they can use the Embulk plugin embulk-input-ftp.
    additionalDependencyDeclarations = [
        [ groupId: "it.sauronsoftware", artifactId: "ftp4j", version: "1.7.2", scope: "compile" ],
    ]
}

javadoc {
    options {
        locale = "en_US"
        encoding = "UTF-8"
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = project.name

            from components.java  // Must be "components.java". The dependency modification works only for it.
            // javadocJar and sourcesJar are added by java.withJavadocJar() and java.withSourcesJar() above.
            // See: https://docs.gradle.org/current/javadoc/org/gradle/api/plugins/JavaPluginExtension.html

            pom {  // https://central.sonatype.org/pages/requirements.html
                packaging "jar"

                name = project.name
                description = project.description
                url = "https://www.embulk.org/"

                licenses {
                    license {
                        // http://central.sonatype.org/pages/requirements.html#license-information
                        name = "The Apache License, Version 2.0"
                        url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }

                developers {
                    developer {
                        name = "Sadayuki Furuhashi"
                        email = "frsyuki@gmail.com"
                    }
                    developer {
                        name = "Muga Nishizawa"
                        email = "muga.nishizawa@gmail.com"
                    }
                    developer {
                        name = "Satoshi Akama"
                        email = "satoshiakama@gmail.com"
                    }
                    developer {
                        name = "Dai MIKURUBE"
                        email = "dmikurube@treasure-data.com"
                    }
                    developer {
                        name = "Shinichi Ishimura"
                        email = "shiketaudonko41@gmail.com"
                    }
                    developer {
                        name = "Phu Nguyen"
                        email = "phu@treasure-data.com"
                    }
                }

                scm {
                    connection = "scm:git:git://github.com/embulk/embulk-input-ftp.git"
                    developerConnection = "scm:git:git@github.com:embulk/embulk-input-ftp.git"
                    url = "https://github.com/embulk/embulk-input-ftp"
                }
            }
        }
    }

    repositories {
        maven {  // publishMavenPublicationToMavenCentralRepository
            name = "mavenCentral"
            if (project.version.endsWith("-SNAPSHOT")) {
                url "https://oss.sonatype.org/content/repositories/snapshots"
            } else {
                url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            }

            credentials {
                username = project.hasProperty("ossrhUsername") ? ossrhUsername : ""
                password = project.hasProperty("ossrhPassword") ? ossrhPassword : ""
            }
        }
    }
}

signing {
    sign publishing.publications.maven
}

gem {
    authors = [ "Sadayuki Furuhashi" ]
    email = [ "frsyuki@gmail.com" ]
    summary = "FTP file input plugin for Embulk"
    homepage = "https://github.com/embulk/embulk-input-ftp"
    licenses = [ "Apache-2.0" ]
    into("classpath/") {
        from "${project.rootDir}/libs/ftp4j-1.7.2.jar"
    }
}

gemPush {
    host = "https://rubygems.org"
}

checkstyle {
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    toolVersion = '6.14.1'
}
checkstyleMain {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
checkstyleTest {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
task checkstyle(type: Checkstyle) {
    classpath = sourceSets.main.output + sourceSets.test.output
    source = sourceSets.main.allJava + sourceSets.test.allJava
}
