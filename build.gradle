plugins {
    id "com.jfrog.bintray" version "1.1"
    id "com.github.jruby-gradle.base" version "0.1.5"
    id "java"
    id "jacoco"
}
import com.github.jrubygradle.JRubyExec

allprojects {
    group = 'org.embulk.input.ftp'
    version = "0.1.5"
}

subprojects {
    apply plugin: 'maven'   // install jar files to the local repo: $ gradle install
    apply plugin: 'java'
    apply plugin: 'com.github.jruby-gradle.base'

    repositories {
        mavenCentral()
        jcenter()
    }

    configurations {
        provided
    }

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    dependencies {
        compile  "org.embulk:embulk-core:0.7.0"
        provided "org.embulk:embulk-core:0.7.0"
        compile files("${project.rootDir}/libs/ftp4j-1.7.2.jar")
        compile "org.bouncycastle:bcpkix-jdk15on:1.52"
        testCompile "junit:junit:4.+"
    }

    task classpath(type: Copy, dependsOn: ["jar"]) {
        doFirst { file("classpath").deleteDir() }
        from (configurations.runtime - configurations.provided + files(jar.archivePath))
        into "classpath"
    }
    clean { delete "classpath" }

    if (![project(":embulk-util-ftp")].contains(project)) {
        task gem(type: JRubyExec, dependsOn: ["build", "gemspec", "classpath"]) {
            jrubyArgs "-rrubygems/gem_runner", "-eGem::GemRunner.new.run(ARGV)", "build"
            script "${project.projectDir.absolutePath}/build/gemspec"
            doLast { ant.move(file: "${project.name}-${project.version}.gem", todir: "${parent.projectDir}/pkg") }
        }
    }

    task "package"(dependsOn: ["gemspec", "classpath"]) << {
        println "> Build succeeded."
        println "> You can run embulk with '-L ${file(".").absolutePath}' argument."
    }
}

task gemPush << {
    "gem push pkg/embulk-input-ftp-${project.version}.gem".execute().waitFor()
}